*** Settings ***
Library           SeleniumLibrary
Suite Teardown    Close All Browsers

*** Variables ***
${URL}            http://localhost:8080
${BROWSER}        Chrome
${USERNAME}       testuser1
${EMAIL}          testuser1@example.com
${PASSWORD}       Secret123!
${POST_TITLE}     Тестовий пост
${POST_DESC}      Короткий опис посту
${POST_BODY}      Це контент тестового посту

*** Test Cases ***
Реєстрація користувача та створення посту
    Відкрити браузер та перейти на сайт
    Перейти на сторінку реєстрації
    Зареєструвати нового користувача
    Увійти в систему
    Перейти на сторінку створення посту
    Заповнити форму створення посту
    Перевірити наявність поста

*** Keywords ***
Відкрити браузер та перейти на сайт
    Open Browser    ${URL}    ${BROWSER}
    Maximize Browser Window
    Wait Until Page Contains    Sign In    5s

Перейти на сторінку реєстрації
    Click Link    xpath=//a[contains(text(),'Or Sign Up')]
    Wait Until Page Contains    Sign Up    5s

Зареєструвати нового користувача
    Input Text    id=username      ${USERNAME}
    Input Text    id=email         ${EMAIL}
    Input Text    id=password      ${PASSWORD}
    Input Text    id=confirm       ${PASSWORD}
    Click Button  id=register
    Wait Until Page Contains    Sign In    5s

Увійти в систему
    Input Text    id=username      ${USERNAME}
    Input Text    id=password      ${PASSWORD}
    Click Button  id=login
    Wait Until Page Contains Element    xpath=//a[contains(text(),'Add New Post')]    5s

Перейти на сторінку створення посту
    Click Link    xpath=//a[contains(text(),'Add New Post')]
    Wait Until Page Contains    Add New Post    5s

Заповнити форму створення посту
    Input Text    id=title         ${POST_TITLE}
    Input Text    id=description   ${POST_DESC}
    Input Text    id=body          ${POST_BODY}
    Click Button  id=post
    Wait Until Page Contains    ${POST_TITLE}    5s

Перевірити наявність поста
    Page Should Contain    ${POST_TITLE}
    Page Should Contain    ${POST_DESC}
    Page Should Contain    ${POST_BODY}